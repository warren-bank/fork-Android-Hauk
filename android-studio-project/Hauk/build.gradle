apply from: '../constants.gradle'
apply plugin: 'com.android.application'

android {
  compileSdkVersion project.ext.compileSdkVersion
  buildToolsVersion project.ext.buildToolsVersion

  compileOptions {
    sourceCompatibility project.ext.javaVersion
    targetCompatibility project.ext.javaVersion
  }

  defaultConfig {
    minSdkVersion project.ext.minSdkVersion
    targetSdkVersion project.ext.targetSdkVersion

    applicationId "info.varden.hauk"
    versionName project.ext.releaseVersion
    versionCode project.ext.releaseVersionCode

    archivesBaseName += "-$versionName"
  }

  buildTypes {
    release {
      debuggable      false
      jniDebuggable   false
      shrinkResources true
      minifyEnabled   true
      proguardFiles = [
        "proguard-rules.txt",
        "proguard-conscrypt.txt",
        getDefaultProguardFile('proguard-android.txt')
      ]
    }
    debug {
      debuggable      true
      jniDebuggable   true
      shrinkResources false
      minifyEnabled   false
    }
  }

  lintOptions {
    disable 'MissingTranslation'
    abortOnError true
  }

  flavorDimensions "languageResources", "securityProvider"

  productFlavors {
    withAllLanguageTranslations {
      dimension "languageResources"
    }
    english {
      dimension "languageResources"
    }
    withSharedExternalConscryptOrDefaultSecurityProvider {
      dimension "securityProvider"
    }
    withInternalConscryptSecurityProvider {
      dimension "securityProvider"
    }
  }

  splits {
    abi {
      enable gradle.startParameter.taskNames.any { it.toLowerCase().contains("withinternalconscryptsecurityprovider") }
      reset()
      include "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
      universalApk false
    }
  }
}

dependencies {
  implementation 'androidx.appcompat:appcompat:'                   + project.ext.libVersionAndroidxAppCompat                // https://mvnrepository.com/artifact/androidx.appcompat/appcompat
  implementation 'androidx.constraintlayout:constraintlayout:'     + project.ext.libVersionAndroidxConstraintLayout         // https://mvnrepository.com/artifact/androidx.constraintlayout/constraintlayout
  implementation 'androidx.preference:preference:'                 + project.ext.libVersionAndroidxPreference               // https://mvnrepository.com/artifact/androidx.preference/preference

  withInternalConscryptSecurityProviderImplementation 'org.conscrypt:conscrypt-android:' + project.ext.libVersionConscrypt  // https://mvnrepository.com/artifact/org.conscrypt/conscrypt-android/2.5.2
}
